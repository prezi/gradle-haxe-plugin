buildscript {
	def gitVersion
	if (hasProperty("release")) {
		gitVersion = [ "git", "describe", "--match", "[0-9]*", "--dirty"].execute().text.trim()
	} else {
		gitVersion = [ "git", "describe", "--match", "[0-9]*", "--abbrev=0"].execute().text.trim() + "-SNAPSHOT"
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		classpath "com.prezi.haxe:gradle-haxe-plugin:${gitVersion}"
	}
}

allprojects {
	group = "prezi.test"
	version = "0.1-SNAPSHOT"

	repositories {
		mavenLocal()

		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url "https://artifactory.prezi.com/repo"
		}
	}
}

subprojects {
	apply plugin: "haxe"

	tasks.withType(Upload) {
		repositories {
			ivy {
				url "file://$rootProject.buildDir/repo"
			}
		}
	}
}
